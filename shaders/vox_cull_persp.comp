#version 450
#extension GL_GOOGLE_include_directive : enable

layout (local_size_x = 64, local_size_y = 1) in;

#include "common.h"

struct Draw_Data {
  float half_size_x;
  float half_size_y;
  float half_size_z;
  uint first_vertex;
  uint first_instance;
  uint vertex_count0;
  uint vertex_count1;
  uint vertex_count2;
  uint vertex_count3;
  uint vertex_count4;
  uint vertex_count5;
  uint cull_mask;
};

struct Transform {
  quat rotation;
  vec3 position;
  float scale;
};

struct Draw_Command {
  uint index_count;
  uint instance_count;
  uint first_index;
  uint vertex_offset;
  uint first_instance;
};

struct Vertex_Count {
  uint count0;
  uint count1;
  uint count2;
  uint count3;
  uint count4;
  uint debug_data1;
  uint debug_data2;
  uint debug_data3;
};

// storage buffer
layout (std140, set = 0, binding = 0) readonly buffer Storage_Buffer {
  Draw_Data in_draws[];
};

layout (std140, set = 0, binding = 1) readonly buffer Transform_Buffer {
  Transform transforms[];
};

layout (std140, set = 0, binding = 2) writeonly buffer Indirect_Buffer {
  Draw_Command out_draws[];
};

layout (std140, set = 0, binding = 3) writeonly buffer Vertex_Buffer {
  Vertex_Count vertex_counts[];
};

PUSH_CONSTANT Pass_Info {
  vec3 camera_front;
  uint cull_mask;
  vec3 camera_position;
  uint draw_offset;
  uint in_offset;
  uint num_draws;
};

const vec3 vox_normals[6] = {
  {-1.0,  0.0,  0.0},
  { 1.0,  0.0,  0.0},
  { 0.0, -1.0,  0.0},
  { 0.0,  1.0,  0.0},
  { 0.0,  0.0, -1.0},
  { 0.0,  0.0,  1.0}
};

void main() {
  uint draw_index = gl_GlobalInvocationID.x;

  // check if we're out of bounds
  if (draw_index >= num_draws)
    return;

  Draw_Data d = in_draws[draw_index + in_offset];

  Transform transform = transforms[d.first_instance];

  uint vertex_count[6] = {
    d.vertex_count0,
    d.vertex_count1,
    d.vertex_count2,
    d.vertex_count3,
    d.vertex_count4,
    d.vertex_count5,
  };

  uint vertex_offset = d.first_vertex;
  uint out_id = (draw_index + draw_offset) * 3;

  // zero out unused drawcalls
  for (int i = 0; i < 3; i++) {
    out_draws[out_id + i].instance_count = 0;
  }
  // frustum culling
  if ((d.cull_mask & cull_mask) == 0) {
    return;
  }

  vec3 box[3];
  box[0] = rotate(vec3(d.half_size_x, 0.0, 0.0), transform.rotation);
  box[1] = rotate(vec3(0.0, d.half_size_y, 0.0), transform.rotation);
  box[2] = rotate(vec3(0.0, 0.0, d.half_size_z), transform.rotation);

  uint last_written_vertex = 0xffffffff;
  uint draw_count = 0;

  // backface culling
  for (int i = 0; i < 6; i++) {
    vec3 basis[3];
    basis[0] = -box[0] * vox_normals[i].x * transform.scale;
    basis[1] = -box[1] * vox_normals[i].y * transform.scale;
    basis[2] = -box[2] * vox_normals[i].z * transform.scale;
    vec3 point;
    point.x = basis[0].x + basis[1].x + basis[2].x + transform.position.x;
    point.y = basis[0].y + basis[1].y + basis[2].y + transform.position.y;
    point.z = basis[0].z + basis[1].z + basis[2].z + transform.position.z;

    vec3 dist = point - camera_position;
    vec3 normal = rotate(vox_normals[i], transform.rotation);
    if (dot(dist, normal) <= 0.0) {
      if (last_written_vertex == vertex_offset) {
        out_draws[out_id + draw_count-1].index_count += vertex_count[i] * 3 / 2;
      } else {
        out_draws[out_id + draw_count].index_count = vertex_count[i] * 3 / 2;
        out_draws[out_id + draw_count].instance_count = 1;
        out_draws[out_id + draw_count].first_index = vertex_offset * 3 / 2;
        out_draws[out_id + draw_count].vertex_offset = d.first_vertex;
        out_draws[out_id + draw_count].first_instance = d.first_instance;
        draw_count++;
      }
      last_written_vertex = vertex_offset + vertex_count[i];
    }
    vertex_offset += vertex_count[i];
  }

  vertex_offset = d.first_vertex;
  vertex_counts[draw_offset + d.first_instance].count0 = (vertex_offset + d.vertex_count0); vertex_offset += d.vertex_count0;
  vertex_counts[draw_offset + d.first_instance].count1 = (vertex_offset + d.vertex_count1); vertex_offset += d.vertex_count1;
  vertex_counts[draw_offset + d.first_instance].count2 = (vertex_offset + d.vertex_count2); vertex_offset += d.vertex_count2;
  vertex_counts[draw_offset + d.first_instance].count3 = (vertex_offset + d.vertex_count3); vertex_offset += d.vertex_count3;
  vertex_counts[draw_offset + d.first_instance].count4 = (vertex_offset + d.vertex_count4); vertex_offset += d.vertex_count4;
}
