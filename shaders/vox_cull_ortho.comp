#version 450
#extension GL_GOOGLE_include_directive : enable

layout (local_size_x = 64, local_size_y = 1) in;

#include "common.h"
#include "culling.h"

// storage buffer
layout (std140, set = 0, binding = 0) readonly buffer Storage_Buffer {
  Draw_Data in_draws[];
};

layout (std140, set = 0, binding = 1) readonly buffer Transform_Buffer {
  Transform transforms[];
};

layout (std140, set = 0, binding = 2) writeonly buffer Indirect_Buffer {
  Draw_Command out_draws[];
};

// TODO: move this to vox_cull_persp.comp
layout (std140, set = 0, binding = 3) writeonly buffer Vertex_Buffer {
  Vertex_Count vertex_counts[];
};

PUSH_CONSTANT Pass_Info {
  mat4 projview_matrix;
  vec3 camera_front;
  uint cull_mask;
  vec3 camera_position;
  uint draw_offset;
  uint in_offset;
  uint num_draws;
};

const vec3 vox_normals[6] = {
  {-1.0,  0.0,  0.0},
  { 1.0,  0.0,  0.0},
  { 0.0, -1.0,  0.0},
  { 0.0,  1.0,  0.0},
  { 0.0,  0.0, -1.0},
  { 0.0,  0.0,  1.0}
};

void main() {
  uint draw_index = gl_GlobalInvocationID.x;

  // check if we're out of bounds
  if (draw_index >= num_draws)
    return;

  Draw_Data d = in_draws[draw_index + in_offset];

  Transform transform = transforms[d.first_instance];

  uint vertex_count[6] = {
    d.vertex_count0,
    d.vertex_count1,
    d.vertex_count2,
    d.vertex_count3,
    d.vertex_count4,
    d.vertex_count5,
  };

  uint vertex_offset = d.first_vertex;
  uint out_id = (draw_index + draw_offset) * 3;

  // zero out unused drawcalls
  for (int i = 0; i < 3; i++) {
    out_draws[out_id + i].instance_count = 0;
  }
  // frustum culling
  if ((d.cull_mask & cull_mask) == 0) {
    return;
  }

  uint last_written_vertex = 0xffffffff;
  uint draw_count = 0;

  vec3 dist = transform.position - camera_position;
  // backface culling
  // Theorem: there're at most 3 drawcalls submitted after this loop.
  for (int i = 0; i < 6; i++) {
    vec3 normal = rotate(vox_normals[i], transform.rotation);
    if (dot(camera_front, normal) <= 0.0) {
      if (last_written_vertex == vertex_offset) {
        out_draws[out_id + draw_count-1].index_count += vertex_count[i] * 3 / 2;
      } else {
        out_draws[out_id + draw_count].index_count = vertex_count[i] * 3 / 2;
        out_draws[out_id + draw_count].instance_count = 1;
        out_draws[out_id + draw_count].first_index = vertex_offset * 3 / 2;
        out_draws[out_id + draw_count].vertex_offset = d.first_vertex;
        out_draws[out_id + draw_count].first_instance = d.first_instance;
        draw_count++;
      }
      last_written_vertex = vertex_offset + vertex_count[i];
    }
    vertex_offset += vertex_count[i];
  }
  vertex_counts[draw_offset + d.first_instance].debug_data1 = draw_count;

  vertex_offset = d.first_vertex;
  vertex_counts[draw_offset + d.first_instance].count0 = (vertex_offset + d.vertex_count0); vertex_offset += d.vertex_count0;
  vertex_counts[draw_offset + d.first_instance].count1 = (vertex_offset + d.vertex_count1); vertex_offset += d.vertex_count1;
  vertex_counts[draw_offset + d.first_instance].count2 = (vertex_offset + d.vertex_count2); vertex_offset += d.vertex_count2;
  vertex_counts[draw_offset + d.first_instance].count3 = (vertex_offset + d.vertex_count3); vertex_offset += d.vertex_count3;
  vertex_counts[draw_offset + d.first_instance].count4 = (vertex_offset + d.vertex_count4); vertex_offset += d.vertex_count4;
}
